\label{AppendixA}

Implementation in ASP of the reasoning algorithm provided by Sharma and Baral \cite{2018CommonsenseKT}.
\begin{lstlisting}[language=Prolog]

%Phase 1: Generating merged representation

%Defining domain of variables
k_val(X) :- has_k(X,R,Y).
k_val(Y) :- has_k(X,R,Y).

%Extracting constant nodes from graphical representation of  
%sentence, question and knowledge (Step 1)
s_const(X) :- has_s(X,instance_of,I).
q_const(X) :- has_q(X,instance_of,I).
k_class(X) :- has_k(A,instance_of,X).
k_const(X) :- not k_class(X), k_val(X).

%Extracting constant nodes which has constant parent nodes from
%graphical representations of sentence, question and knowledge (Step 2)
s_has_par(X) :- has_s(P,R,X), s_const(X), s_const(P).
q_has_par(X) :- has_q(P,R,X), q_const(X), q_const(P).
k_has_par(X) :- has_k(P,R,X), k_const(X), k_const(P).

%Extracting constant nodes which has constant children nodes from
%graphical representations of sentence, question and knowledge. (Step 2)
s_has_child(X) :- has_s(X,R,C), s_const(X), s_const(C).
q_has_child(X) :- has_q(X,R,C), q_const(X), q_const(C).
k_has_child(X) :- has_k(X,R,C), k_const(X), k_const(C).

%Extracting cross-domain siblings from a knowledge representation  
%to a sentence representation. (Step 3)
not_k_s_crossdom_sib(X,Y) :- has_k(X,instance_of,I1),
			has_k(X,instance_of,I2),
			has_s(Y,instance_of,I1),
			not has_s(Y,instance_of,I2),
			I1!=I2.
not_k_s_crossdom_sib(X,Y) :- has_k(X,instance_of,I1),  
			has_k(X,instance_of,I2),
			I1!=I2,
			not has_s(Y,instance_of,I1),
			has_s(Y,instance_of,I2).
not_k_s_crossdom_sib(X,Y) :- has_k(X,instance_of,I1),
			has_k(X,instance_of,I2),
			I1!=I2,
			not has_s(Y,instance_of,I1),
			not has_s(Y,instance_of,I2),
			s_const(Y).
k_s_crossdom_sib(X,Y) :- has_s(Y,instance_of,I),
			has_k(X,instance_of,I),
			s_const(Y),
			k_const(X),
			not not_k_s_crossdom_sib(X,Y).

%Extracting cross-domain clones from a knowledge representation  
%to a sentence representation. (Step 4)
k_s_crossdom_clone(X,Y) :- k_s_crossdom_sib(X,Y),
			has_k(Pj,Rj,X),
			has_s(Pj_prime,Rj,Y),
			k_s_crossdom_clone(Pj,Pj_prime),
			k_const(Pj),
			has_k(X,Rk,Cj),
			has_s(Y,Rk,Cj_prime),
			k_s_crossdom_sib(Cj,Cj_prime),
			k_const(Cj).
k_s_crossdom_clone(X,Y) :- k_s_crossdom_sib(X,Y),
			not k_has_par(X),
			has_k(X,Rk,Cj),has_s(Y,Rk,Cj_prime),
			k_s_crossdom_sib(Cj,Cj_prime),
			k_const(Cj).
k_s_crossdom_clone(X,Y) :- k_s_crossdom_sib(X,Y),
			has_k(Pj,Rj,X),
			has_s(Pj_prime,Rj,Y),
			k_s_crossdom_clone(Pj,Pj_prime),
			k_const(Pj),
			not k_has_child(X).
k_s_crossdom_clone(X,Y) :- k_s_crossdom_sib(X,Y),
			not k_has_par(X),
			not k_has_child(X).

%Generating a merged representation of a sentence and a knowledge. (Step 5)
has_m(X,R,Y) :- has_s(X,R,Y).
k_covered(X) :- k_const(X),
			s_const(Y),
			k_s_crossdom_clone(X,Y).		
k_not_all_covered :- k_const(X),
			not k_covered(X).
k_all_covered :- not k_not_all_covered.
			has_m(X,R,Y) :- has_s(X,R,Y1),
			has_s(X2,R2,Y),
			Y1!=Y,
			k_s_crossdom_clone(Y_k,Y1),
			k_s_crossdom_clone(Y_k,Y),
			k_all_covered.
has_m(X,R,Y) :- has_s(X1,R,Y),
			has_s(X,R2,Y2),
			X1!=X,
			k_s_crossdom_clone(X_k,X1),
			k_s_crossdom_clone(X_k,X),
			k_all_covered.

%Phase 2: Extracting possible answers
 
%Extracting constant nodes from the merged representation. (Step 1)
has_const(X) :- has_m(X,instance_of,I).

%Extracting constant nodes which has constant parent nodes
%from a merged representation. (Step 2)
m_has_par(X) :- has_m(P,R,X), m_const(X), m_const(P).

%Extracting constant nodes which has constant children nodes
%from a merged representation. (Step 2)
m_has_child(X) :- has_m(X,R,C), m_const(X), m_const(C).

%Extracting cross-domain siblings from a question representation
%to a merged representation. (Step 3)
not_q_m_crossdom_sib(X,Y) :- has_q(X,instance_of,I1),
			has_q(X,instance_of,I2),
			has_m(Y,instance_of,I1),
			not has_m(Y,instance_of,I2),
			I1!=I2, I1!=q, I2!=q.
not_q_m_crossdom_sib(X,Y) :- has_q(X,instance_of,I1),
			has_q(X,instance_of,I2),
			not has_m(Y,instance_of,I1),
			has_m(Y,instance_of,I2),
			I1!=I2, I1!=q, I2!=q.
not_q_m_crossdom_sib(X,Y) :- has_q(X,instance_of,I1),
			has_q(X,instance_of,I2),
			not has_m(Y,instance_of,I1),
			not has_m(Y,instance_of,I2),
			m_const(Y),
			I1!=I2, I1!=q, I2!=q.
q_m_crossdom_sib(X,Y) :- has_m(Y,instance_of,I),
			has_q(X,instance_of,I),
			not not_q_m_crossdom_sib(X,Y),
			m_const(Y),
			q_const(X).

%Extracting cross-domain clones from a question representation
%to a merged representation. (Step 4)
q_m_crossdom_clone(X,Y) :- q_m_crossdom_sib(X,Y),
			has_q(Pj,Rj,X),
			has_m(Pj_prime,Rj,Y),
			q_m_crossdom_clone(Pj,Pj_prime),
			q_const(Pj),has_q(X,Rk,Cj),
			has_m(Y,Rk,Cj_prime),
			q_m_crossdom_sib(Cj,Cj_prime),
			q_const(Cj).
q_m_crossdom_clone(X,Y) :- q_m_crossdom_sib(X,Y),
			not q_has_par(X),
			has_q(X,Rk,Cj),
			has_m(Y,Rk,Cj_prime),
			q_m_crossdom_sib(Cj,Cj_prime),
			q_const(Cj).
q_m_crossdom_clone(X,Y) :- q_m_crossdom_sib(X,Y),
			has_q(Pj,Rj,X),
			has_m(Pj_prime,Rj,Y),
			q_m_crossdom_clone(Pj,Pj_prime),
			q_const(Pj),
			not q_has_child(X).
q_m_crossdom_clone(X,Y) :- q_m_crossdom_sib(X,Y),
			not q_has_par(X),
			not q_has_child(X).
			
%Extracting the answers to the input question. (Step 5)
q_covered(X) :- q_const(X),
			m_const(Y),
			q_m_crossdom_clone(X,Y).
			q_not_all_covered :-
			not q_covered(X),
			q_const(X).
q_all_covered :- not q_not_all_covered.
ans(Q,X) :- q_m_crossdom_clone(Q,X),
			has_q(Q,instance_of,q),
			q_all_covered.

%Making answers visible in the terminal
 #show ans/2.
 
\end{lstlisting} 


ASP formalization for WS \#2 and \#8 from the Physical category.

\begin{lstlisting}[language=Prolog]
% WS #2:
%The trophy doesn't fit into the brown suitcase because it's too small.

% Representation:
has_s(it_9,instance_of,entity).
has_s(small_12,is_trait_of,it_9).
has_s(small_12,modifier,too_11).
has_s(too_11,instance_of,too).
has_s(small_12,instance_of,small).
has_s(fit_5,instance_of,fit).
has_s(fit_5,agent,trophy_2).
has_s(fit_5,recipient,suitcase_7).
has_s(suitcase_7,instance_of,entity).
has_s(trophy_2,instance_of,entity).
not has_s(fit_5,modifier,could_3).

% Question:- What is too small?
% Representation:
has_q(small_4,is_trait_of,q_1).
has_q(q_1,instance_of,q).
has_q(q_1,instance_of,entity).
has_q(small_4,instance_of,small).

% Knowledge:- small entity could not fit
% Representation:
has_k(small_1,is_trait_of,y_2) :- has_k(fits_5,recipient,y_2),
				  not has_k(fits_5,modifier,could_3).
has_k(small_1,instance_of,small).
has_k(y_2,instance_of,entity).
has_k(fits_5,instance_of,fit).
has_k(fits_5,recipient,y_2).

%Answer: 
ans(q_1,it_9) ans(q_1,suitcase_7) 

% WS #
% The man could not lift his son because he was so
weak.
% Representation:
has_s(he_9,instance_of,entity).
has_s(weak_12,is_trait_of,he_9).
has_s(weak_12,modifier,so_11).
has_s(so_11,instance_of,so).
has_s(weak_12,instance_of,weak).
has_s(lift_5,instance_of,lift).
has_s(lift_5,agent,man_2).
has_s(lift_5,recipient,son_7).
has_s(son_7,instance_of,entity).
has_s(man_2,instance_of,entity).
has_s(son_7,related_to,his_6).
not has_s(lift_5,modifier,could_3).

% Question:- Who was so weak?
% Representation:
has_q(weak_4,is_trait_of,q_1).
has_q(q_1,instance_of,q).
has_q(q_1,instance_of,entity).
has_q(weak_4,instance_of,weak).

% Knowledge:- weak entity could not lift 
% Representation:
has_k(weak_1, is_trait_of, y_2) :- has_k(lift_5,agent, y_2),  
				   not has_k(lift_5,modifier,could_3).
has_k(weak_1,instance_of,weak).
has_k(y_2,instance_of,entity).
has_k(lift_5, instance_of, lift).
has_k(lift_5, agent, y_2).

%Answer: 
ans(q_1,he_9) ans(q_1,man_2)   

\end{lstlisting}